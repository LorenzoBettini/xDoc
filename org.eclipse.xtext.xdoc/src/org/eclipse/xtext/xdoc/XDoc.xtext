grammar org.eclipse.xtext.xdoc.XDoc hidden(SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
//import "classpath:/model/JavaVMTypes.ecore" as types
import "platform:/resource/org.eclipse.xtext.common.types/model/JavaVMTypes.ecore" as types
generate xdoc "http://www.eclipse.org/xtext/xdoc/Xdoc"

Document:
	{Document}
	WS? MULTI_NL?
	(((sections+=Chapter (MULTI_NL sections+=Chapter)*) |
	(sections+=Section (MULTI_NL sections+=Section)*) |
	(sections+=Section2 (MULTI_NL sections+=Section2)*))
	MULTI_NL?)?;

Chapter:
	('chapter:' name=ID '['| 'chapter[') title=TextOrMarkup ']' 
	(MULTI_NL contents+=TextOrMarkup)*
	(MULTI_NL subSections+=Section)*;

Section:
	('section:' name=ID '['| 'section[') title=TextOrMarkup ']' 
	(MULTI_NL contents+=TextOrMarkup)*
	(MULTI_NL subSections+=Section2)*;

Section2:
	('section2:' name=ID '['| 'section2[') title=TextOrMarkup ']' 
	(MULTI_NL contents+=TextOrMarkup)*
	(MULTI_NL subSections+=Section3)*;

Section3:
	('section3:' name=ID '['|'section3[') title=TextOrMarkup ']' 
	(MULTI_NL contents+=TextOrMarkup)*
	(MULTI_NL subSections+=Section4)*;

Section4:
	('section4:' name=ID '['|'section4[') title=TextOrMarkup ']' 
	(MULTI_NL contents+=TextOrMarkup)*;

AbstractSection :
	Chapter | Section | Section2 | Section3 | Section4 ;

Identifiable:
	AbstractSection | Anchor;

TextOrMarkup:
	(contents+=TextPart
	(contents+=MarkUp contents+=TextPart?)* |
	(contents+=MarkUp contents+=TextPart?)+);

TextPart:
	text=Text;

Text:
	(ID | WS | SINGLE_NL | ANY_OTHER | '\\[' | '\\]')+;

MarkUp:
	Emphasize |
	Anchor |
	Ref |
	OrderedList |
	UnorderedList |
	CodeBlock |
	CodeRef |
	Link |
	ImageRef;

Emphasize:
	'e[' MULTI_NL? contents+=TextOrMarkup MULTI_NL? ']';

Anchor:
	'a[' name=ID ']';

Ref:
	'ref:' ref=[Identifiable] '[' MULTI_NL? contents+=TextOrMarkup MULTI_NL? ']';

OrderedList:
	'ol['
		(ANY_WS* items+=Item)+
		ANY_WS*
	']';

UnorderedList:
	'ul['
		(ANY_WS* items+=Item)+
		ANY_WS*
	']';

Item:
	'item[' MULTI_NL? contents+=TextOrMarkup (MULTI_NL contents+=TextOrMarkup)* ']';

CodeRef:
	'codeRef[' element=[types::JvmIdentifyableElement|Text] ']';

Link:
	'link[' url=Text ']' ANY_WS* '[' text=Text ']';

	// Don't think this was the discussed syntax
ImageRef:
	('img:' ANY_WS* name=ID ANY_WS* '['|'img[') path=Text /*TODO make it a crossreference to images */ ']' ANY_WS*
	'[' clazz=Text ']' ANY_WS* '[' style=Text ']' ANY_WS* '[' caption=Text ']';

CodeBlock:
	'code[' (ANY_WS* language=ID ANY_WS* ']' ANY_WS* '[')? 
		(contents+=Code
		(contents+=MarkupInCode contents+=Code?)* |
		(contents+=MarkupInCode contents+=Code?)+)
	']';

Code:
	contents=CodeText;

MarkupInCode:
	Emphasize |
	Anchor |
	Ref;

CodeText:
	(ID | MULTI_NL | WS | SINGLE_NL | ANY_OTHER | '\\[' | '\\]' )+;

ANY_WS:
	MULTI_NL | SINGLE_NL | WS;

terminal ID:
	('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

terminal SL_COMMENT:
	'%%' !('\n' | '\r')* ('\r'? '\n');

terminal MULTI_NL:
	'\r'? '\n' (/*(' ' | '\t')**/ '\r'? '\n')+;

terminal SINGLE_NL:
	'\r'? '\n';

terminal WS:
	' ' | '\t';

terminal ANY_OTHER:
	.;
