grammar org.eclipse.xtext.xdoc.XDoc hidden (SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "classpath:/model/JavaVMTypes.ecore" as types
generate xdoc "http://www.eclipse.org/xtext/xdoc/Xdoc"

Document:
	{Document}
	((MULTI_NL? chapters+=Chapter)+ |
	(MULTI_NL? sections+=Section)+ |
	(MULTI_NL? subsections+=SubSection)+)
	MULTI_NL?;

Chapter:
	'\\chapter' (':' name=ID)? '[' title=TextOrMarkup ']' WS*
	(MULTI_NL contents+=TextOrMarkup)*
	(MULTI_NL? contents+=Section)*;

Section:
	'\\section' (':' name=ID)? '[' title=TextOrMarkup ']' WS*
	(MULTI_NL contents+=TextOrMarkup)*
	(MULTI_NL? contents+=SubSection)*;

SubSection:
	'\\subsection' (':' name=ID)? '[' title=TextOrMarkup ']' WS*
	(MULTI_NL contents+=TextOrMarkup)*
	(MULTI_NL? contents+=Section3)*;

Section3:
	'\\section3' (':' name=ID)? '[' title=TextOrMarkup ']' WS*
	(MULTI_NL contents+=TextOrMarkup)*
	(MULTI_NL? contents+=Section4)*;

Section4:
	'\\section4' (':' name=ID)? '[' title=TextOrMarkup ']' WS*
	(MULTI_NL contents+=TextOrMarkup)+;

Identifiable:
	Chapter | Section | SubSection | Section3 | Section4 | Anchor;

TextOrMarkup:
	contents+=TextPart
	(contents+=MarkUp contents+=TextPart?)* |
	(contents+=MarkUp contents+=TextPart?)+;

MarkUp:
	Emphasize |
	Anchor |
	Ref |
	OrderedList |
	UnorderedList |
	CodeBlock |
	CodeRef |
	Link |
	ImageRef;

Emphasize:
	'\\emph' ('[' contents+=TextOrMarkup ']');

Anchor:
	'\\a' ':' name=ID ('[' ']')?;

Ref:
	'\\ref' ':' ref=[Identifiable] '[' contents+=TextOrMarkup ']';

OrderedList:
	'\\ol' WS* '[' WS* //WS
		items+=Item+
	']';

UnorderedList:
	'\\ul' WS* '[' WS* //WS
		items+=Item+
	']';

Item:
	'\\item' WS* '[' contents+=TextOrMarkup (MULTI_NL contents+=TextOrMarkup)* ']' WS*;

CodeRef:
	'\\codeRef' '[' element=[types::JvmIdentifyableElement|Text] ']';

Link:
	'\\link' '[' url=Text ']''['text=Text']';
	
ImageRef:
	'\\img'(':' name=ID)? '['path=Text /*TODO make it a crossreference to images */ ']''['clazz=Text']' '['style=Text']' '[' caption=Text']';

CodeBlock:
	'\\code' ('[' language=ID ']')? WS* MULTI_NL?'['
		(contents+=Code
		(contents+=MarkupInCode contents+=Code?)* |
		(contents+=MarkupInCode contents+=Code?)+)
	']';

TextPart:
	text=Text;

Text:
	(ID | WS | ANY_OTHER | '\\[' | '\\]' | ':')+;

CodeText:
	(ID | MULTI_NL | WS | ANY_OTHER | '\\[' | '\\]' | ':')+;

MarkupInCode:
	Emphasize |
	Anchor |
	Ref;

Code:
	contents=CodeText;


terminal ID:
	('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

terminal SL_COMMENT:
	'%%' !('\n' | '\r')* ('\r'? '\n')?;

terminal MULTI_NL:
	('\r'? '\n') (' '|'\t')* ('\r'? '\n') WS*;

terminal WS:
	' ' | '\t' | '\r' | '\n';

terminal ANY_OTHER:
	.;
